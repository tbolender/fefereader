apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

// Load signing information from file
// Source: https://developer.android.com/studio/publish/app-signing.html
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    namespace 'de.timbolender.fefereader'

    compileSdkVersion 33

    defaultConfig {
        applicationId "de.timbolender.fefereader"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 13
        versionName "1.3.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    signingConfigs {
        create("release") {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']

            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    buildTypes {
        getByName("release") {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.getByName("release")
        }

        getByName("debug") {
            applicationIdSuffix ".debug"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildFeatures {
        dataBinding = true
    }

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
}

dependencies {
    def core_version = "1.9.0"
    def room_version = "2.4.3"
    def lifecycle_version = "2.5.1"
    def work_version = "2.7.1"

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation("androidx.core:core-ktx:$core_version")

    implementation 'androidx.appcompat:appcompat:1.5.1'

    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation "androidx.paging:paging-runtime-ktx:2.1.2"
    implementation "androidx.preference:preference-ktx:1.2.0"
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.2.0'

    implementation 'com.google.android.material:material:1.6.1'

    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    testImplementation "androidx.room:room-testing:$room_version"

    implementation "androidx.work:work-runtime-ktx:$work_version"
    androidTestImplementation "androidx.work:work-testing:$work_version"

    // Optional dependency but needed to solve duplicate issues
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"

    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    implementation 'org.jsoup:jsoup:1.13.1'
    implementation 'com.squareup.okhttp3:okhttp:4.5.0'

    implementation 'com.facebook.stetho:stetho:1.5.1'
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.1'

    kaptAndroidTest 'androidx.test.espresso:espresso-core:3.4.0'
    kaptTest 'junit:junit:4.12'
}
